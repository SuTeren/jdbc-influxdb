plugins {
	id 'java-library'
	id 'io.freefair.lombok' version '6.6.3'
	id 'maven-publish'
}
dependencies {
	implementation 'org.influxdb:influxdb-java:2.23'
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'org.slf4j:slf4j-api:2.0.6'

	testImplementation 'org.testcontainers:influxdb:1.17.6'
	testImplementation 'ch.qos.logback:logback-classic:1.4.5'
	testImplementation 'com.tngtech.java:junit-dataprovider:1.13.1'
	testImplementation 'org.apache.commons:commons-collections4:4.4'
	testImplementation 'org.mockito:mockito-core:5.2.0'
}

ext.moduleName = 'InfluxDbSqlDriver'

compileJava {
	inputs.property('moduleName', moduleName)
	doFirst {
		options.compilerArgs = ['--module-path', classpath.asPath]
	}
}

repositories {
	jcenter()
	mavenCentral()
}
java {
	sourceCompatibility = "11"
	targetCompatibility = "11"
	withJavadocJar()
	withSourcesJar()
}

javadoc {
	inputs.property('moduleName', moduleName)
	doFirst {
		options.modulePath = [] + classpath.files
		options.classpath = []
	}
	options {
		//failOnError = false
		encoding = 'UTF-8'
		//addBooleanOption 'Xdoclint:all,-html,-accessibility,-reference', true
	}
}

jar {
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
	duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

repositories {
	maven {
		url = uri("https://maven.pkg.github.com/konikvranik/jdbc-influxdb")
		credentials {
			username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
			password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
		}
	}
	mavenCentral()
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/konikvranik/jdbc-influxdb")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
				password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
			}
		}
	}
	publications {
		maven(MavenPublication) {
			from(project.components.java)
		}
		project.publishing.publications.withType(MavenPublication) {
			pom {
				organization {
					name = 'SuTeren'
					url = 'https://github.com/konikvranik'
				}
				ciManagement {
					system = 'GitHub'
					url = 'https://github.com/konikvranik/jdbc-influxdb/actions'
				}
				issueManagement {
					system = 'GitHub'
					url = 'https://github.com/konikvranik/jdbc-influxdb/issues'
				}
				scm {
					connection = 'scm:git:https://github.com/konikvranik/jdbc-influxdb.git'
					developerConnection = 'scm:git:ssh:git@github.com:konikvranik/jdbc-influxdb.git'
					url = 'https://github.com/konikvranik/jdbc-influxdb/'
				}
			}
		}
	}
}
